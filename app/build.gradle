buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
    }

}

def buildNumber = System.env.CI_JOB_ID ?: "0"
def fabricApiKey = System.env.FABRIC_API_KEY ?: ""

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'io.gitlab.arturbosch.detekt'

if (!fabricApiKey.isEmpty()) {
    apply plugin: 'io.fabric'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    jcenter()
}

android {
    compileSdkVersion 33

    androidExtensions {
        experimental = true
    }

    defaultConfig {
        applicationId "app.devlife.connect2sql"
        minSdkVersion 31
        manifestPlaceholders = [fabricApiKey: fabricApiKey]
        targetSdkVersion 33

        /* format: [  M].[00m].[00p] */
        versionCode 4000015
        versionName '4.0.15'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/services/java.sql.Driver', 'META-INF/maven/com.google.guava/guava/pom.properties', 'META-INF/maven/com.google.guava/guava/pom.xml', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'LICENSE.txt']
        }
    }

    signingConfigs {
        release {
            keyAlias "connect2sql"
            keyPassword System.env.SIGNING_KEY_PASSWORD
            storeFile file("${project.projectDir}/../devtools/signing/release.keystore")
            storePassword System.env.SIGNING_STORE_PASSWORD
        }
        debug {
            keyAlias "ma7in"
            keyPassword System.env.SIGNING_KEY_PASSWORD
            storeFile file("${project.projectDir}/../devtools/signing/ma7in.keystore")
            storePassword System.env.SIGNING_STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "+d${buildNumber}"
            minifyEnabled false
        }

        release {
            versionNameSuffix "+r${buildNumber}"
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {

    }


    detekt {
        version = '1.22.0'
        defaultProfile {
            config = file("$projectDir/detekt.yml")
            output = file("$buildDir/reports/")
            outputName = "detekt-report"
        }
    }
    lint {
        abortOnError true
        lintConfig file("$projectDir/lint.xml")
        quiet false
    }
    namespace 'com.gitlab.connect2sql'
}

ext {
    version_arch = '1.1.1'
    version_answers = '1.4.3'
    version_constraint_layout = '1.1.3'
    version_crashlytcs = '2.9.5'
    version_dagger = '2.16'
    version_jsr250 = '1.0'
    version_jsch = '0.1.54'
    version_logback_android = '1.1.1-12'
    version_expandable_rv = '1.3'
    version_mariadb = '1.7.4'
    version_dexmaker_mockito = '2.19.0'
    version_patternlock = '2.1.2'
    version_rx = '1.1.0'
    version_saripaar = '1.0.3'
    version_sqlcipher = '3.5.9'
    version_slf4j = '1.7.25'
    version_support = '28.0.0'
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "android.arch.lifecycle:extensions:${version_arch}"
    implementation "android.arch.lifecycle:viewmodel:${version_arch}"
    //noinspection GradleCompatible
    implementation "com.android.support:support-v4:${version_support}"
    implementation "com.android.support:support-annotations:${version_support}"
    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:${version_support}"
    //noinspection GradleCompatible
    implementation "com.android.support:cardview-v7:${version_support}"
    implementation "com.android.support.constraint:constraint-layout:${version_constraint_layout}"
    //noinspection GradleCompatible
    implementation "com.android.support:customtabs:${version_support}"
    //noinspection GradleCompatible
    implementation "com.android.support:design:${version_support}"
    //noinspection GradleCompatible
    implementation "com.android.support:recyclerview-v7:${version_support}"
    //noinspection OutdatedLibrary
    implementation("com.crashlytics.sdk.android:answers:${version_answers}@aar") {
        transitive = true
    }
    implementation "com.github.tony19:logback-android:${version_logback_android}"
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    kapt "com.google.dagger:dagger-android-processor:${version_dagger}"
    kapt "com.google.dagger:dagger-compiler:${version_dagger}"
    implementation "com.google.dagger:dagger:${version_dagger}"
    //noinspection OutdatedLibrary
    implementation "com.google.dagger:dagger-android:${version_dagger}"
    implementation "com.google.dagger:dagger-android-support:${version_dagger}"
    implementation "com.jcraft:jsch:${version_jsch}"
    implementation "com.linkedin.dexmaker:dexmaker-mockito:${version_dexmaker_mockito}"
    implementation "com.mobsandgeeks:android-saripaar:${version_saripaar}"
    implementation "com.thoughtbot:expandablerecyclerview:${version_expandable_rv}"
    implementation "javax.annotation:jsr250-api:${version_jsr250}"
    implementation "io.reactivex:rxandroid:${version_rx}"
    implementation "io.reactivex:rxjava:${version_rx}"
    implementation "me.zhanghai.android.patternlock:library:${version_patternlock}"
    implementation "net.zetetic:android-database-sqlcipher:${version_sqlcipher}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${version_kotlin}"
    implementation "org.mariadb.jdbc:mariadb-java-client:${version_mariadb}"
    implementation "org.slf4j:slf4j-api:${version_slf4j}"
}
